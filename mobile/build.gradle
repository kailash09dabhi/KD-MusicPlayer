apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

apply plugin: 'de.mobilej.unmock'
unMock {
    keep "android.widget.BaseAdapter"
    keep "android.widget.ArrayAdapter"
    keep "android.os.Bundle"
    keepStartingWith "android.database.MatrixCursor"
    keep "android.database.AbstractCursor"
    keep "android.database.CrossProcessCursor"
    keepStartingWith "android.text.TextUtils"
    keepStartingWith "android.util."
    keepStartingWith "android.text."
    keepStartingWith "android.content.ContentValues"
    keepStartingWith "android.content.ComponentName"
    keepStartingWith "android.content.ContentUris"
    keepStartingWith "android.content.ContentProviderOperation"
    keepStartingWith "android.content.ContentProviderResult"
    keepStartingWith "android.content.UriMatcher"
    keepStartingWith "android.content.Intent"
    keep "android.location.Location"
    keepStartingWith "android.content.res.Configuration"
    keepStartingWith "org."
    keepStartingWith "libcore."
    keepStartingWith "com.android.internal.R"
    keepStartingWith "com.android.internal.util."
    keep "android.net.Uri"

    keepAndRename "java.nio.charset.Charsets" to "xjava.nio.charset.Charsets"
}
repositories {
    maven { url 'https://maven.fabric.io/public' }
}
apply from: "$project.rootDir/tools/script-git-version.gradle"
apply from: "$project.rootDir/tools/script-findbugs.gradle"
apply from: "$project.rootDir/tools/script-lint.gradle"
apply from: "$project.rootDir/tools/script-pmd.gradle"
apply from: "$project.rootDir/tools/script-java-code-coverage.gradle"

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = false
        }
        resValue "string", "admob_app_id", "ca-app-pub-1642663068953785~5782766159"
        resValue "string", "kd_music_player_small_native_banner",
                "ca-app-pub-1642663068953785/6701096157"
        resValue "string", "kd_music_player_banner",
                "ca-app-pub-1642663068953785/7201696550"
        resValue "string", "kd_music_player_settings_interstitial",
                "ca-app-pub-1642663068953785/7259499352"
        resConfigs "en"
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    flavorDimensions "kdmusicplayer"
    productFlavors {
        free {
            applicationId "com.kingbull.kdmusicplayer"
            versionCode gitVersionCode
            versionName gitVersionName
        }
        pro {
            applicationId "com.kingbull.kdmusicplayer.pro"
            versionCode gitVersionCode
            versionName gitVersionName
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            ext.enableCrashlytics = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    compileOptions.incremental = false
}

def supportLibraryVersion = "27.0.2";
def daggerLibraryVersion = "2.11";
def butterknifeLibraryVersion = "8.8.1";
def rxVersion = "2.1.0";
def rxAndroidVersion = "2.0.1";
def facebookVersion = "4.23.0";
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //  wearApp project(':wear')
    //  compile 'com.google.android.gms:play-services:9.8.0'
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:support-compat:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:palette-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"


    implementation "com.jakewharton:butterknife:$butterknifeLibraryVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeLibraryVersion"

    implementation "com.google.dagger:dagger:$daggerLibraryVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerLibraryVersion"

    implementation "com.github.nisrulz:recyclerviewhelper:$supportLibraryVersion"

    implementation ('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
    implementation 'com.github.javiersantos:PiracyChecker:1.1'

    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxVersion"
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'

    implementation 'com.simplecityapps:recyclerview-fastscroll:1.0.16'
    implementation 'com.github.Commit451:NativeStackBlur:1.0.5'
    implementation 'uk.co.chrisjenx:calligraphy:2.3.0'
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.github.bumptech.glide:glide:4.5.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.5.0'

    implementation 'com.github.linger1216:labelview:v1.1.2'

    freeImplementation 'com.google.android.gms:play-services-ads:11.8.0'
    freeImplementation "com.facebook.android:facebook-android-sdk:$facebookVersion"
    freeImplementation "com.facebook.android:audience-network-sdk:$facebookVersion"
    freeImplementation 'com.google.ads.mediation:facebook:4.23.0.0'


    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.7.22'
    androidTestImplementation  'com.android.support.test.espresso:espresso-contrib:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    unmock 'org.robolectric:android-all:6.0.0_r1-robolectric-0'
}
//configurations.all{
//    resolutionStrategy{
//        force "com.android.support:recyclerview-v7:$supportLibraryVersion"
//        force "com.android.support:cardview-v7:$supportLibraryVersion"
//    }
//}
// make sure this line comes *after* you apply the Android plugin
apply plugin: 'com.getkeepsafe.dexcount'