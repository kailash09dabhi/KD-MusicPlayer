apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.dicedmelon.gradle.jacoco-android'

apply plugin: 'de.mobilej.unmock'
unMock {
    keep "android.widget.BaseAdapter"
    keep "android.widget.ArrayAdapter"
    keep "android.os.Bundle"
    keepStartingWith "android.database.MatrixCursor"
    keep "android.database.AbstractCursor"
    keep "android.database.CrossProcessCursor"
    keepStartingWith "android.text.TextUtils"
    keepStartingWith "android.util."
    keepStartingWith "android.text."
    keepStartingWith "android.content.ContentValues"
    keepStartingWith "android.content.ComponentName"
    keepStartingWith "android.content.ContentUris"
    keepStartingWith "android.content.ContentProviderOperation"
    keepStartingWith "android.content.ContentProviderResult"
    keepStartingWith "android.content.UriMatcher"
    keepStartingWith "android.content.Intent"
    keep "android.location.Location"
    keepStartingWith "android.content.res.Configuration"
    keepStartingWith "org."
    keepStartingWith "libcore."
    keepStartingWith "com.android.internal.R"
    keepStartingWith "com.android.internal.util."
    keep "android.net.Uri"

    keepAndRename "java.nio.charset.Charsets" to "xjava.nio.charset.Charsets"
}
repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
    google()
    maven { url 'https://maven.google.com' }
}
apply from: "$project.rootDir/tools/script-git-version.gradle"
apply from: "$project.rootDir/tools/script-lint.gradle"

android {
    compileSdkVersion 34

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 18
        versionName "0.8.1"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = false
        }
        resValue "string", "admob_app_id", "ca-app-pub-1642663068953785~5782766159"
        resValue "string", "kd_music_player_small_native_banner",
                "ca-app-pub-1642663068953785/6701096157"
        resValue "string", "kd_music_player_banner",
                "ca-app-pub-1642663068953785/7201696550"
        resValue "string", "kd_music_player_settings_interstitial",
                "ca-app-pub-1642663068953785/7259499352"
      resConfigs 'en'
    }
  packagingOptions {
    resources {
      excludes += ['META-INF/rxjava.properties']
    }
  }
  flavorDimensions "kdmusicplayer"
    productFlavors {
        free {
            applicationId "com.kingbull.kdmusicplayer"
            versionCode getVersionCode()
            versionName getVersionName()
        }
        pro {
            applicationId "com.kingbull.kdmusicplayer.pro"
            versionCode getVersionCode()
            versionName getVersionName()
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            ext.enableCrashlytics = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
  lint {
    htmlOutput file("$project.buildDir/outputs/lint/lint.html")
    lintConfig file("$rootDir/tools/rules-lint.xml")
    warningsAsErrors true
    xmlReport false
  }
  namespace 'com.kingbull.musicplayer'
  compileOptions.incremental = false
}

def supportLibraryVersion = "27.1.1"
def daggerLibraryVersion = "2.28.3"
def rxVersion = "2.2.9"
def rxAndroidVersion = "2.1.0"
def facebookVersion = "16.2.0"
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //  wearApp project(':wear')
    //  compile 'com.google.android.gms:play-services:9.8.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core:1.12.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'com.google.android.material:material:1.9.0'


    implementation "com.jakewharton:butterknife:10.1.0"
    implementation platform('com.google.firebase:firebase-bom:29.0.3')
    implementation 'com.google.firebase:firebase-crashlytics:18.4.1'
    implementation 'com.google.firebase:firebase-analytics:21.3.0'
    annotationProcessor "com.jakewharton:butterknife-compiler:10.1.0"

    implementation "com.google.dagger:dagger:$daggerLibraryVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerLibraryVersion"

    implementation "com.github.nisrulz:recyclerviewhelper:$supportLibraryVersion"

    implementation 'com.github.javiersantos:PiracyChecker:1.1'

    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:${rxVersion}"
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.0'

    implementation 'com.simplecityapps:recyclerview-fastscroll:1.0.16'
    implementation 'com.github.Commit451:NativeStackBlur:1.0.7'
    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation 'androidx.media:media:1.6.0@aar'
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
    implementation 'com.github.linger1216:labelview:v1.1.2'
    implementation 'io.github.inflationx:viewpump:2.0.3'
    implementation 'io.github.inflationx:calligraphy3:3.1.1'
    freeImplementation 'com.google.android.gms:play-services-ads:22.3.0'
    freeImplementation "com.facebook.android:facebook-android-sdk:$facebookVersion"
    freeImplementation "com.facebook.android:audience-network-sdk:6.16.0"
    freeImplementation 'com.google.ads.mediation:facebook:6.16.0.0'
    constraints {

        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.22")
    }

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.6.28'
    androidTestImplementation  'androidx.test.espresso:espresso-contrib:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    unmock 'org.robolectric:android-all:9-robolectric-4913185-2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    annotationProcessor("javax.annotation:javax.annotation-api:1.3.2")
}

def getVersionCode = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'tag', '--list'
            standardOutput = code
        }
        return code.toString().split("\n").size()
    }
    catch (ignored) {
        return -1;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}
// make sure this line comes *after* you apply the Android plugin
